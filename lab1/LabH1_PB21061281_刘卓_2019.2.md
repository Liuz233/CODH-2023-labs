# Lab 1 report

PB21061281			刘卓

### 实验目的与内容

* 熟练掌握ALU的功能
* 掌握数据通路和控制器的设计方法
* 掌握组合电路和时许逻辑电路，以及参数化和结构化Verilog描述方法
* 了解查看电路性能和资源使用情况

1.完成ALU模块的逻辑设计和仿真

2.完成MAV的逻辑设计、仿真和下载测试，并查看RTL电路图，以及实现电路资源和时间性能报告

3.选项：完成32位ALU的下载测试，并查看RTL电路图，以及实现电路资源和时间性能报告

### 逻辑设计

#### part1

核心代码:ALU减法模块，作减法并设置比较标志

注意以下几个点

1.verilog变量默认无符号数，可以定义signed变量代表有符号数

2.无符号数是没有符号位的，比如3'b111>3'b010，但经过实验，若变量为有符号数，则结果为3'b111<3'b010，即verilog的'<'可以根据两边变量类型确定结果，所以设置标志位直接用对应变量求<的结果即可

3.算数右移和逻辑右移的区别:逻辑右移是直接向右移动，高位补0，而对于逻辑右移，如果是负数，高位会补1，也就实现了有符号数的除法，而verilog变量默认无符号数，所以逻辑右移和算数右移结果相同，需要用signed变量赋值或者直接$signed

```verilog
y=a-b;
            signed_a=a;
            signed_b=b;
            if(y==0) t[0]=1;
            else     t[0]=0;
            if(a<b)  t[2]=1;
            else     t[2]=0;
            if(signed_a<signed_b) t[1]=1;
            else     t[1]=0;
```

#### part2

![image-20230405001549615](C:\Users\Liuz\Desktop\CODH lab\lab1\LabH1_PB21061281_刘卓_2019.2.assets\image-20230405001549615.png)

<center>图1:各部分框图及数据通路

![image-20230405001636306](C:\Users\Liuz\Desktop\CODH lab\lab1\LabH1_PB21061281_刘卓_2019.2.assets\image-20230405001636306.png)

<center>图2:状态机的状态转移图</center>

核心代码：2段FSM部分，这里使用四个寄存器记录最近四个数，其本质与cs、ns一样，是一个状态，所以一共需要两组共8个寄存器记录(nm1,nm2,nm3,nm4,m1,m2,m3,m4)，细节见代码注释

这里bug调试了很久，主要有以下问题

1.button_edge信号是输入信号，只在2段FSM确定下一状态的过程出现，不决定输出以及cs<=ns这一过程

2.FSM组合逻辑模块一开始，要对所有“下一状态”值赋一个默认值，否则任意一个状态在后续case、if、else过程中没有照顾到，就会出现锁存器，最终出问题。这里赋了默认值后，后面就不用考虑再赋默认值了

```verilog
    always@(posedge clk,posedge rstn)
    begin
        if(rstn) 
        begin
            cs<=3'b0;
            m1<=0;m2<=0;m3<=0;m4<=0;
        end
        else 
        begin
            cs<=ns;
            m1<=nm1;m2<=nm2;m3<=nm3;m4<=nm4;
        end
    end
    always@(*)
    begin
        nm1=m1;nm2=m2;nm3=m3;nm4=m4;//非常重要，默认不发生转移
        ns=cs;
        case(cs)
        3'b000:begin
            m=0;//Moore型输出不受输入影响，只与状态有关
            if(button_edge) 
            begin
                nm1=0;nm2=0;nm3=0;nm4=d;
                ns=3'b001;
            end
            else ns=cs;
        end
        3'b001:begin
            m=m4;
            if(button_edge) 
            begin
                nm1=0;nm2=0;nm3=m4;nm4=d;
                ns=3'b010;
            end
            else ns=cs;
        end
        3'b010:begin
            m=m4;
            if(button_edge) 
            begin
                nm1=0;nm2=m3;nm3=m4;nm4=d;
                ns=3'b011;
            end
            else ns=cs;
        end
        3'b011:begin
            m=m4;
            if(button_edge) 
            begin
                nm1=m2;nm2=m3;nm3=m4;nm4=d;
                ns=3'b100;
            end
            else ns=cs;
        end
        3'b100:begin
            m=(m1+m2+m3+m4)>>2;
            if(button_edge) 
            begin
                nm1=m2;nm2=m3;nm3=m4;nm4=d;
                ns=3'b100;
            end
            else ns=cs;
        end
        default: begin
            m=(m1+m2+m3+m4)>>2;
            if(button_edge) 
            begin
                nm1=m2;nm2=m3;nm3=m4;nm4=d;
                ns=3'b100;
            end
            else ns=cs;
        end
        endcase
    end
```



### 仿真结果与分析

#### part1

![image-20230330193603617](C:\Users\Liuz\Desktop\CODH lab\lab1\LabH1_PB21061281_刘卓_2019.2.assets\image-20230330193603617.png)

<center>图3:ALU仿真结果截图

仿真测试文件根据时钟信号，循环42次，每次时钟上升沿到来时，对$a,b$取32位随机数，$f$则在$0-7$中循环变化，观察各类运算结果，ALU设计正确

#### part2

![image-20230405105555560](C:\Users\Liuz\Desktop\CODH lab\lab1\LabH1_PB21061281_刘卓_2019.2.assets\image-20230405105555560.png)

<center>图4:MAV仿真结果截图</center>

图中en为模拟的按钮信号，可能持续多个时钟，button_edge为去毛刺以及取边沿后的时钟信号，可以看见，在第一个button_edge上升沿到来时，表示状态机下一状态的ns和nm4均发生变化，在下一时钟上升沿到来时，时序逻辑模块更新cs和m4，同时cs变化，组合逻辑模块里的m也会变化。后面情况以此类推



仿真文件如下

```verilog
`timescale  1ns / 1ps

module tb_MAV();

// MAV Parameters
parameter PERIOD  = 10;


// MAV Inputs
reg   clk                                  = 0 ;
reg   rstn                                 = 0 ;
reg   en                                   = 0 ;
reg   [15:0]  d                             = 0 ;

// MAV Outputs
wire  [3:0]  seg                           ;
wire  [2:0]  AN                            ;
integer seed;

initial
begin
    clk=0;
    forever #(PERIOD/2)  clk=~clk;
end

initial
begin
    rstn=0;
    #5 rstn  =  1;
    #5 rstn=0;
end

MAV  u_MAV (
    .clk                     ( clk                ),
    .rstn                    ( rstn               ),
    .en                      ( en                 ),
    .d                       ( d            [15:0] ),

    .seg                     ( seg          [3:0] ),
    .AN                      ( AN           [2:0] )
);

initial  begin seed = 0; end//随机数种子
initial
begin
    repeat(10000)
    begin
        @(posedge clk)
        begin
            en=$random(seed)%2;
        end
    end
    //$finish;
end
initial
begin
    d=16'habcd;//赋初始值，否则一开始就是0，不利于观察
    repeat(10000)
    #100 d=$random(seed);//延迟100是为了模拟板子上的情况

    //$finish;
end
endmodule
```



### 电路设计与分析

#### part1

![image-20230331205626196](C:\Users\Liuz\Desktop\CODH lab\lab1\LabH1_PB21061281_刘卓_2019.2.assets\image-20230331205626196.png)

<center>图5:ALU模块RTL电路图

#### part2

![image-20230405111425205](C:\Users\Liuz\Desktop\CODH lab\lab1\LabH1_PB21061281_刘卓_2019.2.assets\image-20230405111425205.png)

<center>图6:MAV模块RTL电路图

​    又电路图可知，主要部分是m1-m4,nm1-nm4共八个寄存器，根据6个状态的6选1选择器，以及加法器等等

![image-20230405112209706](C:\Users\Liuz\Desktop\CODH lab\lab1\LabH1_PB21061281_刘卓_2019.2.assets\image-20230405112209706.png)

<center>图7:综合/实现电路

实际就是把RTL电路展开，比如m4寄存器展开为8个1位寄存器，缓冲器等等

![image-20230405112922076](C:\Users\Liuz\Desktop\CODH lab\lab1\LabH1_PB21061281_刘卓_2019.2.assets\image-20230405112922076.png)

<center>图8:MAV模块资源使用率</center>

![image-20230405113005350](C:\Users\Liuz\Desktop\CODH lab\lab1\LabH1_PB21061281_刘卓_2019.2.assets\image-20230405113005350.png)

<center>图9:工程总体资源使用率

由图9可见，工程中IO模块的资源使用率最高

![image-20230405114026268](C:\Users\Liuz\Desktop\CODH lab\lab1\LabH1_PB21061281_刘卓_2019.2.assets\image-20230405114026268.png)

<center>图10:时序电路综合分析

最长建立时间5.268ns，最长路径均为从cnt_reg[8]出发到cnt_reg[i]

最长保持时间0.152ns，脉冲长度4.5ns

### 测试结果与分析

以下是根据PPT上样例测试MAV的过程，经过下载测试，功能正确

<img src="C:\Users\Liuz\Desktop\CODH lab\lab1\LabH1_PB21061281_刘卓_2019.2.assets\1680666990081.jpg" alt="1680666990081" style="zoom:50%;" />

<img src="C:\Users\Liuz\Desktop\CODH lab\lab1\LabH1_PB21061281_刘卓_2019.2.assets\1680666990071.jpg" alt="1680666990071" style="zoom:50%;" />

<img src="C:\Users\Liuz\Desktop\CODH lab\lab1\LabH1_PB21061281_刘卓_2019.2.assets\1680666990043.jpg" alt="1680666990043" style="zoom:50%;" />

<img src="C:\Users\Liuz\Desktop\CODH lab\lab1\LabH1_PB21061281_刘卓_2019.2.assets\1680666990052.jpg" alt="1680666990052" style="zoom:50%;" />



<img src="C:\Users\Liuz\Desktop\CODH lab\lab1\LabH1_PB21061281_刘卓_2019.2.assets\1680666990062.jpg" alt="1680666990062" style="zoom:50%;" />

<img src="C:\Users\Liuz\Desktop\CODH lab\lab1\LabH1_PB21061281_刘卓_2019.2.assets\1680666990033.jpg" alt="1680666990033" style="zoom:50%;" />

### 总结

任务:编写ALU的verilog，编写去毛刺和取时钟边沿模块，编写MAV的verilog，仿真与下载

上学期在普通班做实验，这次感觉自己verilog功底比较差，对于这种“并发”的语言理解差，出了一些bug，也调了很久。

希望老师详细讲一下怎么根据RTL,Utilization,Time report等电路综合报告来评价和分析电路，这方面完全不懂

### 附件

1.ALU模块

```verilog
`timescale 1ns / 1ps
//////////////////////////////////////////////////////////////////////////////////
// Company: 
// Engineer: 
// 
// Create Date: 2023/03/29 20:14:48
// Design Name: 
// Module Name: ALU_16
// Project Name: 
// Target Devices: 
// Tool Versions: 
// Description: 
// 
// Dependencies: 
// 
// Revision:
// Revision 0.01 - File Created
// Additional Comments:
// 
//////////////////////////////////////////////////////////////////////////////////


module ALU_32#(parameter WIDTH = 32)(
    input [WIDTH-1:0]a,b,
    input [2:0] f,
    output reg[WIDTH-1:0] y,
    output reg[2:0] t
);
    reg signed [WIDTH-1:0] signed_a;
    reg signed [WIDTH-1:0] signed_b;
    always@(*)
    begin
        signed_a=0;
        signed_b=0;
        t=0;
        case(f)
        0:begin
            y=a-b;
            signed_a=a;
            signed_b=b;
            if(y==0) t[0]=1;
            else     t[0]=0;
            if(a<b)  t[2]=1;
            else     t[2]=0;
            if(signed_a<signed_b) t[1]=1;
            else     t[1]=0;

        end  
        1:begin
            y=a+b;
        end
        2:begin
            y=a&b;
        end  
        3:begin
            y=a|b;
        end  
        4:begin
            y=a^b;
        end 
        5:begin
            y=a>>b[4:0];
        end  
        6:begin
            y=a<<b[4:0];
        end
        7:begin
            signed_a=a;
            y=signed_a>>>b[4:0];
        end
        default:begin
            signed_a=a;
            y=signed_a>>>b[4:0];
        end    
        endcase 
    end
endmodule

```

2.去毛刺模块

```verilog
module jitter_clr(
    input clk,
    input button,
    output button_clean
);
     reg  [3:0] cnt;
     always@(posedge clk)
     begin
      if(button==1'b0)
         cnt <= 4'h0;
      else if(cnt<4'h8)
         cnt<=cnt+1'b1;
     end 
     assign button_clean = cnt[3];
endmodule
```

3.取时钟边沿模块

```verilog
module signal_edge(
    input clk,
    input button,
    output button_edge
);
    wire button_clean;
    jitter_clr jt(//这里信号要先调用去毛刺模块处理
         .clk(clk),
         .button(button),
         .button_clean(button_clean)
     );
    reg button_r1,button_r2;
    always@(posedge clk)
    button_r1 <= button_clean;
    always@(posedge clk)
    button_r2 <= button_r1;
    assign button_edge = button_r1 & (~button_r2);
endmodule
```

4.MAV

```verilog
`timescale 1ns / 1ps
//////////////////////////////////////////////////////////////////////////////////
// Company: 
// Engineer: 
// 
// Create Date: 2023/04/03 11:35:37
// Design Name: 
// Module Name: MAV
// Project Name: 
// Target Devices: 
// Tool Versions: 
// Description: 
// 
// Dependencies: 
// 
// Revision:
// Revision 0.01 - File Created
// Additional Comments:
// 
//////////////////////////////////////////////////////////////////////////////////


module MAV(
    input clk,
    input rstn,
    input en,
    input  [15:0] d,
    output reg [7:0] seg,
    output reg [7:0] AN//
    );
    reg  [15:0] m;
    reg  [31:0] cnt;
    wire button_edge;
    wire pulse_50hz;
    reg  [15:0] nm1,nm2,nm3,nm4;
    reg  [15:0] m1,m2,m3,m4;
    reg  [2:0] cs,ns;
    reg  [3:0] seg_data;
    reg  [1:0] cnt_seg;
    always@(posedge clk)
    begin
        if(rstn)
        cnt<=32'b0;
        else if(cnt>=1000)//
        cnt<=32'b0;
        else cnt<=cnt+32'b1;
    end
    assign pulse_50hz=(cnt==32'b1);//产生用于分时复用的信号
    signal_edge ed(
        .clk(clk),
        .button(en),
        .button_edge(button_edge)
    );
    always @(posedge clk)
    begin
        if(rstn)
            cnt_seg<=2'b00;
        else if(pulse_50hz)
            cnt_seg<=cnt_seg+1;
    end
    //这里是2段式FSM
    always@(posedge clk,posedge rstn)
    begin
        if(rstn) 
        begin
            cs<=3'b0;
            m1<=0;m2<=0;m3<=0;m4<=0;
        end
        else 
        begin
            cs<=ns;
            m1<=nm1;m2<=nm2;m3<=nm3;m4<=nm4;
        end
    end
    always@(*)
    begin
        nm1=m1;nm2=m2;nm3=m3;nm4=m4;
        ns=cs;
        case(cs)
        3'b000:begin
            m=0;
            if(button_edge) 
            begin
                nm1=0;nm2=0;nm3=0;nm4=d;
                ns=3'b001;
            end
            else ns=cs;
        end
        3'b001:begin
            m=m4;
            if(button_edge) 
            begin
                nm1=0;nm2=0;nm3=m4;nm4=d;
                ns=3'b010;
            end
            else ns=cs;
        end
        3'b010:begin
            m=m4;
            if(button_edge) 
            begin
                nm1=0;nm2=m3;nm3=m4;nm4=d;
                ns=3'b011;
            end
            else ns=cs;
        end
        3'b011:begin
            m=m4;
            if(button_edge) 
            begin
                nm1=m2;nm2=m3;nm3=m4;nm4=d;
                ns=3'b100;
            end
            else ns=cs;
        end
        3'b100:begin
            m=(m1+m2+m3+m4)>>2;
            if(button_edge) 
            begin
                nm1=m2;nm2=m3;nm3=m4;nm4=d;
                ns=3'b100;
            end
            else ns=cs;
        end
        default: begin
            m=(m1+m2+m3+m4)>>2;
            if(button_edge) 
            begin
                nm1=m2;nm2=m3;nm3=m4;nm4=d;
                ns=3'b100;
            end
            else ns=cs;
        end
        endcase
    end
    //end
    always @(cnt_seg)//控制分时复用，这个模块后面也可以用
    begin
  
        case (seg_data )//七段数码管显示
        4'd0 : seg <= 8'b11000000;   //0
        4'd1 : seg <= 8'b11111001;   //1
        4'd2 : seg <= 8'b10100100;   //2
        4'd3 : seg <= 8'b10110000;   //3
        4'd4 : seg <= 8'b10011001;   //4
        4'd5 : seg <= 8'b10010010;   //5
        4'd6 : seg <= 8'b10000010;   //6
        4'd7 : seg <= 8'b11111000;   //7
        4'd8 : seg <= 8'b10000000;   //8
        4'd9 : seg <= 8'b10010000;   //9
        4'ha : seg <= 8'b00001000;   //a
        4'hb : seg <= 8'b00000000;   //b
        4'hc : seg <= 8'b01000110;   //c
        4'hd : seg <= 8'b01000000;   //d
        4'he : seg <= 8'b00000110;   //e
        4'hf : seg <= 8'b00001110;   //f
        default : seg <= 8'b11000000;   //0
        endcase
 
         case (cnt_seg)
        2'b00: begin AN<= 8'b11111110;seg_data<=m[3:0] ; end
        2'b01: begin AN<= 8'b11111101;seg_data<=m[7:4]; end
        2'b10: begin AN <= 8'b11111011;seg_data<=m[11:8] ; end
        2'b11: begin  AN <= 8'b11110111; seg_data<=m[15:12]; end
        default:AN <= 8'b11110000;
        endcase
   
        if(rstn) 
        begin
            AN <= 8'b11111110;
            seg_data<=0;
        end
  
    end
endmodule

```

5.tb_MAV(仿真测试文件)

```verilog
`timescale  1ns / 1ps

module tb_MAV();

// MAV Parameters
parameter PERIOD  = 10;


// MAV Inputs
reg   clk                                  = 0 ;
reg   rstn                                 = 0 ;
reg   en                                   = 0 ;
reg   [15:0]  d                             = 0 ;

// MAV Outputs
wire  [3:0]  seg                           ;
wire  [2:0]  AN                            ;
integer seed;

initial
begin
    clk=0;
    forever #(PERIOD/2)  clk=~clk;
end

initial
begin
    rstn=0;
    #5 rstn  =  1;
    #5 rstn=0;
end

MAV  u_MAV (
    .clk                     ( clk                ),
    .rstn                    ( rstn               ),
    .en                      ( en                 ),
    .d                       ( d            [15:0] ),

    .seg                     ( seg          [3:0] ),
    .AN                      ( AN           [2:0] )
);

initial  begin seed = 0; end
initial
begin
    repeat(10000)
    begin
        @(posedge clk)
        begin
            en=$random(seed)%2;
        end
    end
    //$finish;
end
initial
begin
    d=16'habcd;
    repeat(10000)
    #100 d=$random(seed);

    //$finish;
end
endmodule
```

